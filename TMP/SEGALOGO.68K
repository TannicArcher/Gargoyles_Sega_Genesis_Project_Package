SegaLogoCHR		incbin	data\shell\segalogo.cpk
			even

SegaLogo		lea	SegaLogoCHR,a0		;compressed chr data
			moveq	#32,d6			;DMA Char data
			bsr	BlitCharset

			move.l	#ScrlB_Base,d6		;screen base address
			moveq	#12,d1			;x,y screen pos
			moveq	#10,d2
			bsr	GetScreenAddrs		;calc screen address
			lea	SegaLogoMAP,a0		;logo map
			moveq	#11,d0			;width
			moveq	#3,d1			;height
			moveq	#0,d4			;add
			bsr	windowblock		;print block to screen

			move.w	#$18,PixX_Copy		;position it

			move	#$2300,sr		;enable irq
			st.b	service			
			lea	SegaLogoPAL,a1		;fade
			jsr	lite_fader
			jsr	wait_fade

			moveq	#$28,d7
SegaLogoLOOP		jsr	WaitFrm	 		;cycle the sega logo
			jsr	WaitFrm
			lea	Pal1+4,a1
			lea	SegaLogoCYCLE(pc,d7),a0
			moveq	#11-1,d0
SegaLogoCRAM		move.w	(a0)+,(a1)+
			dbra	d0,SegaLogoCRAM
			subq.w	#2,d7
			bpl.s	SegaLogoLOOP

			move.w	#3*hz5060,d1		;wait 5 secs
			jsr	frame_delay2		
			jsr	dark_fader		;fade
			jmp	wait_fade

SegaLogoCYCLE		DC.W	$EC0,$EA0,$E80,$E60,$E40,$E20,$E00,$C00
			DC.W	$A00,$800,$600,$800,$A00,$C00,$E00,$E20
			DC.W	$E40,$E60,$E80,$EA0,$EC0,$EA0,$E80,$E60
			DC.W	$E40,$E20,$E00,$C00,$A00,$800,$600
		
SegaLogoPAL		DC.W	$000,$eee
			DC.W	$ec0,$ea0,$e80,$e60,$e40,$e20,$e00,$c00
			DC.W	$a00,$800,$600,$800,$a00,$c00,$e00,$e20
			DC.W	$e40,$e60,$e80,$ea0,$ec0,$ea0,$e80,$e60
			DC.W	$e40,$e20,$e00,$c00,$a00,$800,$600
			ds.w	31

SegaLogoMAP		DC.W	1,2,3,4,5,6,7,8,9,10,11,12
			DC.W	13,14,15,16,17,18,19,20,21,22,23,24
			DC.W	25,26,27,28,29,30,31,32,33,34,35,36
			DC.W	37,38,39,40,41,42,43,44,45,46,47,48	
				       
